apply plugin: 'kotlin'
apply plugin: 'eu.appsatori.fatjar'
apply plugin: 'jacoco'
apply plugin: "maven"
apply plugin: "signing"


group = 'io.github.cobayo'
archivesBaseName='yunas-framework'
version = '1.0.2'
description = 'a kotlin web framework'

repositories {
    mavenCentral()
}

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'eu.appsatori:gradle-fatjar-plugin:0.3'
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.1.1'
    }
}

dependencies {
    compile 'org.eclipse.jetty.aggregate:jetty-all:9.1.4.v20140401'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'com.zaxxer', name: 'HikariCP', version: '2.6.3'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.1'
    compile group: 'org.thymeleaf', name: 'thymeleaf', version: '3.0.7.RELEASE'
    compile group: 'org.thymeleaf.extras', name: 'thymeleaf-extras-java8time', version: '3.0.0.RELEASE'
    compile group: 'org.jetbrains', name: 'annotations', version: '13.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
    testCompile "org.jetbrains.kotlin:kotlin-stdlib:1.1.1"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:1.1.1"
    testCompile group: 'mysql', name: 'mysql-connector-java', version: '6.0.6'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.8.4'
}

//create a single Jar with all dependencies
fatJar {
    baseName = archivesBaseName
}

// For Maven Central
def sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
def isRelease
if (hasProperty("release")) {
    isRelease = true
}
task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from "${buildDir}/javadoc"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required{isRelease}
    sign configurations.archives
}

uploadArchives {
    repositories {
            mavenDeployer {

                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }


                repository(url: sonatypeRepositoryUrl) {
                    authentication(userName: sonatypeUsername, password: sonatypePassword)
                }

                pom.project {
                    name 'yunas'
                    packaging 'jar'
                    description 'A kotlin web framework'
                    url 'https://cobayo.github.io/yunas'

                    scm {
                        url "scm:git@github.com:cobayo/yunas.git"
                        connection "scm:git@github.com:cobayo/yunas.git"
                        developerConnection "scm:git@github.com:cobayo/yunas.git"
                    }
                    licenses {
                        license {
                            name 'Apache License'
                            url 'http://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }
                    developers {
                        developer {
                            id 'yunas'
                            name 'Yosuke Kobayashi'
                            email 'yunasframework@gmail.com'
                        }
                    }
                }
            }
    }
}



